module eu.esrocos.kul.GenerateDsl

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

// The following variables can be overridden when invoking this workflow
var outDir  = "."
var grammar = "grammar.xtext"
var root    = "."
var extension = "mydsl"


var runtimeProject = outDir
var grammarURI     = grammar
var encoding       = "UTF-8"


Workflow {
    component = XtextGenerator {
        configuration = {
            project = StandardProjectConfig {
                baseName = runtimeProject
                rootPath = root
                runtimeTest = {
                    enabled = false
                }
                eclipsePlugin = {
                    enabled = false
                }
                eclipsePluginTest = {
                    enabled = false
                }
                createEclipseMetaData = false
            }
            code = {
                encoding = encoding
                fileHeader = "/*\n * generated by Xtext \${version}\n */"
            }
        }
        language = StandardLanguage {
            fileExtensions = extension
            grammarUri = grammarURI

            serializer = {
                generateStub = false
            }
            validator = {
                // composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
            }
        }
    }
}